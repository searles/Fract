// Labels of of extern parameters
val maxIterationLabel = "Maximum Iteration Count";
val functionLabel = "Function (z[n])";
val maxExponentLabel = "Maximum Exponent";
val isJuliaSetLabel = "Julia Set";
val juliaSetParameterLabel = "Julia Set Parameter";
val z0Label = "Start Value (z[0])";
val useBailoutLabel = "Use Bailout (unset for eg Newton Fractals)";
val bailoutRadiusLabel = "Bailout Radius";
val bailoutValueLabel = "Bailout Value";
val bailoutColorValueLabel = "Bailout Color Value";
val bailoutHeightLabel = "Bailout Height";
val bailoutPaletteLabel = "Bailout Palette";
val epsilonRadiusLabel = "Epsilon Radius";
val lakeValueLabel = "Lake Value";
val lakeColorValueLabel = "Lake Color Value";
val lakeHeightLabel = "Lake Height";
val lakePaletteLabel = "Lake Palette";

val addendFunctionLabel = "Addend Function";
val sumStartValueLabel = "Start Value of Sum";
val startIndexLabel = "Start Index of Sum";

setScale(2, 0, 0, 2, 0, 0); // Default Scale

class Calculator {
    extern maxIteration: maxIterationLabel = "250";

    var sum: Cplx;

    var n: Int = 0; // count iterations
    var c: Cplx;    // parameter
    var z: Cplx;
    var lastZ: Cplx = 0:0; // z[n-1]
    var nextZ: Cplx; // z[n+1]
    var radZ: Real; // |z[n]|
    var dz: Cplx;   // z[n+1] - z[n]
    var radDz: Real; //  |dz|

    fun init() {
        extern isJuliaSet: isJuliaSetLabel = "false";
        c = if(isJuliaSet) {
            extern juliaSetParameter: juliaSetParameterLabel = "-0.75: 0.25";
            juliaSetParameter
        } else {
            point;
        }

        z = if(isJuliaSet) {
            point
        } else {
            extern z0: z0Label = "0";
            z0;
        }

        extern sumStartValue: sumStartValueLabel = "0";
        sum = sumStartValue;
    }

    /*
    * Performs one full calculation step
    * z, lastZ and n are modified.
    * true is returned if none of the break conditions succeeds.
    */
    fun step() {
        extern function: functionLabel = "z^2 + c";
        nextZ = function;

        radZ = rad nextZ;
        dz = nextZ - z;
        radDz = rad dz;
    }

    extern addendFunction: addendFunctionLabel = "norm nextZ ^ 3";
    extern startIndex: startIndexLabel = "0";

    fun advance() {
        if(n > startIndex) {
          sum = sum + addendFunction;
        }

        lastZ = z;
        z = nextZ;
    }

    val lakePaletteIndex = addPalette(lakePaletteLabel, 1, 1, [0, 0, #ff000000]);

    extern lakeValue: lakeValueLabel = "radDz : radZ";
    extern lakeColorValue: lakeColorValueLabel = "value";
    extern lakeHeight: lakeHeightLabel = "re value";

    fun isMaxIteration() {
        if(not next(maxIteration, n)) {
            var value = lakeValue;
            setResult(lakePaletteIndex, lakeColorValue, lakeHeight);
            true
        } else {
            false
        }
    }

    fun isEpsilon() {
        extern epsilonRadius: epsilonRadiusLabel = "1e-9";

        if(radDz < epsilonRadius) {
            var value = lakeValue;
            setResult(lakePaletteIndex, lakeColorValue, lakeHeight);

            true
        } else {
            false
        }
    }

    fun isBailout() {
        val paletteIndex = addPalette(bailoutPaletteLabel, 4, 6,
            [0, 0, #ff000000], [0, 1, #ff000000], [0, 2, #ff000000], [0, 3, #ff000000], [0, 4, #ff000000], [0, 5, #ff000000],
            [1, 0, #ffff0000], [1, 1, #ffffaa00], [1, 2, #ffffff00], [1, 3, #ff00bb44], [1, 4, #ff0044ff], [1, 5, #ff7700ff],
            [2, 0, #ffffffff], [2, 1, #ffffffff], [2, 2, #ffffffff], [2, 3, #ffffffff], [2, 4, #ffffffff], [2, 5, #ffffffff],
            [3, 0, #ffff0000], [3, 1, #ffffaa00], [3, 2, #ffffff00], [3, 3, #ff00bb44], [3, 4, #ff0044ff], [3, 5, #ff7700ff]
        );

        extern bailoutRadius: bailoutRadiusLabel = "1000000";

        if(radZ >= bailoutRadius) {
            extern maxExponent: maxExponentLabel = "2";

            // smoothness value for fractal polynoms
            var continuousAddend =
                1 - log(log radZ / log bailoutRadius) / log maxExponent;

            var count = n + continuousAddend - startIndex;
            var branchingAverage = (sum + continuousAddend * addendFunction) / count;

            extern bailoutValue: bailoutValueLabel = "re branchingAverage : log count";
            extern bailoutColorValue: bailoutColorValueLabel = "value";
            extern bailoutHeight: bailoutHeightLabel = "re value";

            var value = bailoutValue;
            setResult(paletteIndex, bailoutColorValue, bailoutHeight);

            true
        } else {
            false
        }
    }

    fun isBreak() {
        isBailout() or
        isEpsilon() or
        isMaxIteration()
    }
}

val calculator = Calculator();

calculator.init();

while ({
    calculator.step();
    not calculator.isBreak()
}) {
    calculator.advance();
}
