// Define name of extern parameters
val maxIterationLabel = "Maximum Iteration Count";
val functionLabel = "Function (z[n])";
val maxExponentLabel = "Maximum Exponent";
val isJuliaSetLabel = "Julia Set";
val juliaSetParameterLabel = "Julia Set Parameter";
val z0Label = "Start Value (z[0])";
val useBailoutLabel = "Use Bailout (unset for eg Newton Fractals)";
val bailoutRadiusLabel = "Bailout Radius";
val bailoutValueLabel = "Bailout Value";
val bailoutColorValueLabel = "Bailout Color Value";
val bailoutHeightLabel = "Bailout Height";
val bailoutPaletteLabel = "Bailout Palette";
val epsilonRadiusLabel = "Epsilon Radius";
val lakeValueLabel = "Lake Value";
val lakeColorValueLabel = "Lake Color Value";
val lakeHeightLabel = "Lake Height";
val lakePaletteLabel = "Lake Palette";

val aggregateFunctionLabel = "Aggregate Function";
val aggregateStartValueLabel = "Start Value of Aggregate Function";
val startIndexLabel = "Start Index of Aggregation";

// Define default scale and palette
declareScale(2, 0, 0, 2, 0, 0);
declarePalette(lakePaletteLabel, 1, 1, [0, 0, #ff000000]);

extern maxIteration: maxIterationLabel = "250";

class Calculator {

  extern aggregateStartValue: aggregateStartValueLabel = "0";
  var aggregateValue: Cplx = aggregateStartValue;

  // iteration counter
  var n: Int = 0;

  extern isJuliaSet: isJuliaSetLabel = "false";

  // parameter
  var c: Cplx = if(isJuliaSet) {
    extern juliaSetParameter: juliaSetParameterLabel = "-0.75: 0.25";
    juliaSetParameter
  } else {
    point;
  }

  // z[n]
  var z: Cplx = if(isJuliaSet)
    point
  else {
    extern z0: z0Label = "0";
    z0;
  }

  // z[n-1]
  var lastZ: Cplx = 0:0;

  /*
   * Performs one full calculation step
   * z, lastZ and n are modified.
   * true is returned if none of the break conditions succeeds.
   */
  fun step() {
    extern function: functionLabel = "z^2 + c";
    var nextZ = function;

    var radZ = rad nextZ;
    var dz = nextZ - z;
    var radDz = rad dz;

    extern aggregateFunction: aggregateFunctionLabel = "aggregateValue + exp(-radZ): log(1 + radDz)";
    extern startIndex: startIndexLabel = "0";

    if(n > startIndex) {
        aggregateValue = aggregateFunction;
    }

    extern useBailout: useBailoutLabel = "true";

    if(useBailout) {
        declarePalette(bailoutPaletteLabel, 6, 1,
          [0, 0, #ff000000], [1, 0, #ff4400aa], [5, 0, #ff4400aa], [2, 0, #ffffdd22], [4, 0, #ffffdd22], [3, 0, #ffffffff]);

        extern bailoutRadius: bailoutRadiusLabel = "64";
    }

    if(if(useBailout) radZ >= bailoutRadius else false) {
      extern maxExponent: maxExponentLabel = "2";

      // bailout
      extern bailoutValue: bailoutValueLabel = "log (1 + re aggregateValue)";
      extern bailoutColorValue: bailoutColorValueLabel = "value";
      extern bailoutHeight: bailoutHeightLabel = "re value";

      var value = bailoutValue;
      setResult(1, bailoutColorValue, bailoutHeight);

      false
    } else {
      extern epsilonRadius: epsilonRadiusLabel = "1e-9";

      extern lakeValue: lakeValueLabel = "log (1 + im aggregateValue)";
      extern lakeColorValue: lakeColorValueLabel = "value";
      extern lakeHeight: lakeHeightLabel = "re value";

      if(radDz < epsilonRadius) {
        var value = lakeValue;
        setResult(0, lakeColorValue, lakeHeight);

        false
      } else if(not next(maxIteration, n)) {
        // lake
        var value = lakeValue;
        setResult(0, lakeColorValue, lakeHeight);

        false
      } else {
        // next step.
        lastZ = z;
        z = nextZ;

        true
      }
    }
  }
}

// Main starts here. It is rather simple and linear.

val calculator = Calculator()

while (
  calculator.step()
);